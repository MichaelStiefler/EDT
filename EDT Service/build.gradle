apply plugin: 'com.android.application'

// Load keystore
def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId 'jp.co.casio.edt.service'
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 100001
        versionName '1.00'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        archivesBaseName = "EDT v$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // Specifies one flavor dimension.
    flavorDimensions "device"
    productFlavors {
        itg400 {
            // Assigns this product flavor to the "device" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "device"
            //applicationIdSuffix ".itg400"
            versionNameSuffix "-itg400"
        }
        itg600 {
            dimension "device"
            //applicationIdSuffix ".itg600"
            versionNameSuffix "-itg600"
        }
        itg650 {
            dimension "device"
            //applicationIdSuffix ".itg650"
            versionNameSuffix "-itg650"
        }
        dtx400 {
            dimension "device"
            //applicationIdSuffix ".dtx400"
            versionNameSuffix "-dtx400"
        }
        etl10 {
            dimension "device"
            //applicationIdSuffix ".etl10"
            versionNameSuffix "-etl10"
        }
    }


    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

repositories {
    mavenCentral()
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    itg400Implementation project(path: ':DeviceLibraryItg400')
    itg600Implementation project(path: ':DeviceLibraryItg6x0')
    itg650Implementation project(path: ':DeviceLibraryItg6x0')
    dtx400Implementation project(path: ':DeviceLibraryDtx400')
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation project(path: ':EDT Library')
    implementation group: 'commons-io', name: 'commons-io', version: '20030203.000550'
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation 'com.google.code.gson:gson:2.8.6'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}